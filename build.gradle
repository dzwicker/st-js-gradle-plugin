defaultTasks 'clean', 'check', 'assemble'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.bmuschko.nexus'

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

def javaVersion = 1.8
def defaultEncoding = 'UTF-8'

ext {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    slf4j_version = '1.7.2'
}

group = 'com.github.dzwicker.stjs.gradle'

compileJava.options.encoding = defaultEncoding
compileTestJava.options.encoding = defaultEncoding

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'net.researchgate:gradle-release:2.0.0'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
    }
}

idea {
    module {

        //if you prefer different output folders
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main")
        testOutputDir = file("$buildDir/classes/test")
        downloadJavadoc = true
        downloadSources = true

        excludeDirs += [file(".gradle")]
    }
}

repositories {
    mavenCentral()
}

dependencies {

    compile gradleApi()

    compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4j_version
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4j_version

    compile group: 'org.st-js', name: 'generator', version: '3.2.2'
    compile group: 'com.google.guava', name: 'guava', version: '17.0'
}

createReleaseTag.dependsOn uploadArchives

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
}
modifyPom {
    project {
        name 'Gradle ST-JS plugin'
        packaging 'jar'
        description 'Gradle plugin that provides tasks to compile java classes with the st-js generator to javascript.'
        url 'https://github.com/dzwicker/st-js-gradle-plugin'
        inceptionYear '2014'

        scm {
            url 'https://github.com/dzwicker/st-js-gradle-plugin'
            connection 'scm:https://dzwicker@github.com/dzwicker/st-js-gradle-plugin'
            developerConnection 'scm:git://github.com/dzwicker/st-js-gradle-plugin'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'dzwicker'
                name 'Daniel Zwicker'
                email 'email-at-daniel-zwicker-com'
            }
        }
    }
}
